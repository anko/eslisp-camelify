(var runTest (function (name func)
 ((require "tape") name
                   (function (t)
                             (func t)
                             ((. t end)))))) ; Automatically end tests

(var eslc
 (function (code)
           (var fullCode ; wrap in a call
                (+ "(macro camelify (require \"./index.js\")) (camelify "
                   code
                   ")"))
           (return ((require "eslisp") fullCode))))

(macro test
 (function (name body)
  (return `(runTest ,name (function (t) ,@body)))))

; - - -

(test "var"    (((. t equals) (eslc "what-is-this")   "whatIsThis;")))
(test "number" (((. t equals) (eslc "34")   "34;")))
(test "string" (((. t equals) (eslc "\"hi\"")   "'hi';")))

(test "call"     (((. t equals) (eslc "(what-is-this)") "whatIsThis();")))
(test "nested"   (((. t equals) (eslc "(a (a-b 2))") "a(aB(2));")))
(test "capitals" (((. t equals) (eslc "inner-HTML") "innerHTML;")))
(test "many dashes"      (((. t equals) (eslc "hey------there") "heyThere;")))
(test "many statements"  (((. t equals) (eslc "(a-b) (c-d)") "aB();\ncD();")))
(test "underscores kept" (((. t equals) (eslc "a_b") "a_b;")))
(test "lead and trail"   (((. t equals) (eslc "-a-") "A;")))
(test "kept in numbers"  (((. t equals) (eslc "-42") "-42;")))
(test "kept in plain dashes"  (((. t equals) (eslc "(- (-- a) -1)") "--a - -1;")))

(test "preserves unquote-splicing atom"
      (((. t equals)
        (eslc "(macro wrap (function (x) (return `(foo ,@x)))) (wrap (a b))")
        "foo(a, b);")))

(test "also works on invalid names if transformed later"
      (((. t equals)
        (eslc "(macro x (function () (return `a))) (return (x a-b.c))")
        "return a;")))

(test "no-op"     (((. t equals) (eslc "(whatIsThis (whatElse what))")
                                 "whatIsThis(whatElse(what));")))
